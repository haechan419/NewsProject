plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco' // JaCoCo 플러그인 추가
}

group = 'com'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot Core
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-json'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'com.sun.mail:jakarta.mail:2.0.1'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
	
	// Swagger/OpenAPI
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
	
	// Crawling / Feed
	implementation 'org.jsoup:jsoup:1.17.2'
	implementation 'com.rometools:rome:2.1.0'
	
	// 캐싱 (Caffeine)
	implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
	
	// 이미지 썸네일 (Thumbnailator)
	implementation 'net.coobird:thumbnailator:0.4.20'
	
	// Drive Mode
	implementation 'org.modelmapper:modelmapper:3.1.1'

	// JSON 직렬화 (Gson)
	implementation 'com.google.code.gson:gson:2.10.1'

	// Jackson
	implementation 'com.fasterxml.jackson.core:jackson-databind'

//	// DB
	// runtimeOnly 'com.mysql:mysql-connector-j'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'

	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport // 테스트 후 자동으로 커버리지 리포트 생성
}

// JVM Encoding
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

tasks.named('bootRun') {
	systemProperty 'file.encoding', 'UTF-8'
	systemProperty 'console.encoding', 'UTF-8'
	systemProperty 'user.language', 'ko'
	systemProperty 'user.country', 'KR'
	// Windows CMD에서 한글 출력을 위한 JVM 옵션
	jvmArgs = [
		'-Dfile.encoding=UTF-8',
		'-Dconsole.encoding=UTF-8',
		'-Duser.language=ko',
		'-Duser.country=KR'
	]
}

// JaCoCo 설정
jacoco {
	toolVersion = "0.8.11"
}

jacocoTestReport {
	dependsOn test
	
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
	
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				'**/config/**',
				'**/domain/**',
				'**/dto/**',
				'**/*Application.class'
			])
		}))
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.70 // 70% 커버리지 목표
			}
		}
	}
}
